module Std.Test for
  Runner,
  Result,
  Status,
  Case

import Std.Core for System, Reflect

class Runner {

  init() {
    this.result = Result()
    this.cases = []
  }

  add(c) {
    this.cases.push(c)
    return this
  }

  print(value) {
    System.print(value)
  }

  run() {
    this.result = Result()
    for (var i = 0; i < this.cases.length; i = i + 1) {
      var c = this.cases[i]
      this.runCase(c)
    }
    this.print(this.result.toString())
    return this.result.wasSuccessful()
  }

  runCase(c) {
    var className = Reflect.getClassName(c)
    var methods = Reflect.getMethodNames(c)
    var before = this.getBeforeMethods(c)
    var after = this.getAfterMethods(c)

    this.print('Class: ' + className + ' ')

    methods.forEach { | methodName |
      var method = Reflect.getMethod(c, methodName)
      var testInfo = Reflect.getMetadata(method, 'test')
      
      if (testInfo != null) {

        var label = '';
        if (testInfo.length > 0) {
          label = testInfo.join('\n')
        }

        c.currentTest = Status(className, methodName)
        before.forEach { it() }

        try {
          method()
          if (c.currentTest.isDone()) {
            c.currentTest.success = true
            this.print('.')
          } else {
            c.currentTest.success = false
            c.currentTest.error = "(warning) no assert"
            this.print('W')
          }
        } catch (e) {
          this.print('E')
          if (Reflect.getType(e) == 'Status') {
            e = e.error
          }
          c.currentTest.error = "exception thrown: " + e +
            '\n' + label
        }

        after.forEach { it() }
        this.result.add(c.currentTest)
      }
    }

    this.print('\n')
  }

  getBeforeMethods(c) {
    return this.getMarkedMethods(c, 'before')
  }

  getAfterMethods(c) {
    return this.getMarkedMethods(c, 'after')
  }

  getMarkedMethods(c, name) {
    var marked = [];
    Reflect.getMethodNames(c).forEach { | methodName |
      var method = Reflect.getMethod(c, methodName)
      var meta = Reflect.getMetadata(method, name)
      if (meta != null) {
        marked.push(method)
      }
    }
    return marked
  }

}

class Result {

  init() {
    this.tests = []
    this.failures = 0
    this.success = true
  }

  add(status) {
    this.tests.push(status)
    if (status.success == false) {
      this.success = false
    }
  }

  wasSuccessful() {
    return this.success
  }

  toString() {
    var out = ""
    for (var i = 0; i < this.tests.length; i = i + 1) {
      var test = this.tests[i]
      if (test.success == false) {
        out = out + "* " + test.className + "::" + test.methodName + "()\n"
        out = out + "ERR: " + test.error + "\n"
        this.failures = this.failures + 1
      }
    }

    out = out + "\n"
    if (this.failures == 0) {
      out = out + 'OK '
    } else {
      out = out + 'FAILED '
    }
    out = out + this.tests.length + ' tests, ' +
      this.failures + ' failed, ' +
      (this.tests.length - this.failures) + ' success \n'

    return out
  }

}

class Status {

  init(className, methodName) {
    this.className = className
    this.methodName = methodName
    this.success = false
    this.error = null
    this.done = false
  }

  markDone() {
    this.done = true
  }

  isDone() {
    return this.done
  }

}

class Case {

  init() {
    this.currentTest = Status()
  }

  assertTrue(value) {
    this.currentTest.done = true
    if (value != true) {
      this.currentTest.success = false
      this.currentTest.error = "Expected true but was false"
      throw this.currentTest
    }
  }

  assertFalse(value) {
    this.currentTest.done = true
    if (value != false) {
      this.currentTest.success = false
      this.currentTest.error = "Expected false but was true"
      throw this.currentTest
    }
  }

  assertEquals(expected, actual) {
    this.currentTest.done = true
    if (expected != actual) {
      this.currentTest.success = false
      this.currentTest.error = "Expected '" + expected + "' but was '" + actual + "'"
      throw this.currentTest
    }
  }

}
