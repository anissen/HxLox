module Std.Core for Reflect, System, Meta

/// `System` implementation for the js target
class System {

  static print(s) {
    if (process != null) {
      // Print WITHOUT a newline in Node environments
      process.stdout.write(s)
    } else {
      // Web -- note that this will add a newline
      console.log(s)
    }
  }

  static println(s) {
    this.print(s + '\n')
  }

  static getCwd() {
    if (process != null) {
      return process.cwd()
    }
    throw 'Not available on JS target'
  }

}

var _toString = ({}).toString

/// `Reflect` implementation for the js target. This depends on
/// the `lib/js-lib.js` file to work.
class Reflect {
  
  static getClass(target) {
    return __quirk.classes[this.getClassName(target)]
  }

  static getType(target) {
    if (_toString.call(target) == '[object Array]') {
      return 'Array'
    }
    // todo: more primitive checks
    return this.getClassName(target)
  }

  static getClassName(target) {
    return target.__name
  }

  static getSuperclass(target) {
    if (target.prototype != null) {
      return target.prototype.__super
    }
    return target.__super
  }

  static is(a, b) {
    var passing = this.getType(a) == this.getType(b)
    if (passing != true) {
      var superclass = this.getSuperclass(a)
      if (superclass == null) return false
      return this.is(superclass, b)
    }
    return passing
  }

  static getMethod(target, name) {
    var f = target[name]
    if (f && f.bind) {
      return f.bind(target)
    }
    return f
  }

  static getMethodNames(target) {
    var cls = this.getClass(target)
    if (cls == null) return []
    return Object.getOwnPropertyNames(cls.prototype) 
  }

  static getField(target, name) {
    return target[name]
  }

  static getFieldNames(target) {
    return Object.keys(target)
  }

}

class Meta {
  
  static getFields(cls) {
    var name = Reflect.getClassName(cls)
    var sup = Reflect.getSuperclass(cls)
    var meta = __quirk.meta[name]
    
    if (meta == null) meta = {}

    if (sup != null) {
      // todo: will need to handle deep merging
      return Object.assign({}, this.getFields(sup), meta)
    }

    return meta
  }

  static getType(cls) {
    return this.getFields(cls).__TYPE__
  }

}
